AC_INIT([monodevelop], 1.9.1, [monodevelop-list@lists.ximian.com])
AC_PREREQ(2.53)
AM_INIT_AUTOMAKE([1.9 tar-ustar])
AM_MAINTAINER_MODE

ASSEMBLY_VERSION=1.9.1.0
PACKAGE_VERSION_LABEL="2.0 Alpha 2"

AC_PATH_PROG(MONO, mono)
AC_PATH_PROG(MCS, gmcs)

if test "x$MONO" = "x" ; then
	AC_MSG_ERROR([Can't find "mono" in your PATH])
fi

if test "x$MCS" = "x" ; then
  AC_MSG_ERROR([Can't find "gmcs" in your PATH])
fi
AC_SUBST(PATH)
AC_SUBST(LD_LIBRARY_PATH)

dnl Add MD's mimetypes to mime and desktop databases
AC_ARG_ENABLE(update-mimedb,
   AC_HELP_STRING([--disable-update-mimedb],
                   [disable the update-mime-database after install [default=no]]),,
    enable_update_mimedb=yes)
 AM_CONDITIONAL(ENABLE_UPDATE_MIMEDB, test x$enable_update_mimedb = xyes)

if test "x$enable_update_mimedb" = "xyes"; then
  AC_PATH_PROG(UPDATE_MIME_DB, update-mime-database, no)
  if test "x$UPDATE_MIME_DB" = "xno"; then
    AC_MSG_ERROR([You need to install update-mime-database])
  fi
fi
AC_SUBST(UPDATE_MIME_DB)

AC_ARG_ENABLE(update-desktopdb,
   AC_HELP_STRING([--disable-update-desktopdb],
                  [disable the update-desktop-database after install [default=no]]),,
                  enable_update_desktopdb=yes)
   AM_CONDITIONAL(ENABLE_UPDATE_DESKTOPDB, test x$enable_update_desktopdb = xyes)

if test "x$enable_update_desktopdb" = "xyes"; then
  AC_PATH_PROG(UPDATE_DESKTOP_DB, update-desktop-database, no)
  if test "x$UPDATE_DESKTOP_DB" = "xno"; then
    AC_MSG_ERROR([You need to install update-desktop-database])
  fi
fi
AC_SUBST(UPDATE_DESKTOP_DB)

dnl Find pkg-config
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
        AC_MSG_ERROR([You need to install pkg-config])
fi

dnl Find msgfmt for translations
# Find msgfmt and msgmerge for translations
AC_PATH_PROG(MSGFMT, msgfmt, no)
if test "x$MSGFMT" = "xno"; then
        AC_MSG_ERROR([You need to install msgfmt from intltool])
fi
AC_PATH_PROG(MSGMERGE, msgmerge, no)
if test "x$MSGMERGE" = "xno"; then
        AC_MSG_ERROR([You need to install msgmerge from intltool])
fi

MONO_REQUIRED_VERSION=2.0
PKG_CHECK_MODULES(UNMANAGED_DEPENDENCIES_MONO,mono >= $MONO_REQUIRED_VERSION, has_mono=true, has_mono=false)

if test "x$has_mono" = "xfalse"; then
      AC_MSG_ERROR([Please install mono version $MONO_REQUIRED_VERSION or later to install MonoDevelop.
Please see http://www.mono-project.org/ to download latest mono sources or packages])
fi

if test "x$has_mono" = "xtrue"; then
    if test `uname -s` = "Darwin"; then
	AC_PATH_PROG(RUNTIME, mono, no)
	AC_PATH_PROG(CSC, gmcs, no)
	LIB_PREFIX=
	LIB_SUFFIX=.dylib
    else
	AC_PATH_PROG(RUNTIME, mono, no)
	AC_PATH_PROG(CSC, gmcs, no)
	LIB_PREFIX=.so
	LIB_SUFFIX=
    fi
fi


dnl hard dependencies
MONOADDINS_REQUIRED_VERSION=0.3
GTKSHARP_REQUIRED_VERSION=2.8.0
MONODOC_REQUIRED_VERSION=1.0

PKG_CHECK_MODULES(MONO_ADDINS, mono-addins >= $MONOADDINS_REQUIRED_VERSION)
AC_SUBST(MONO_ADDINS_LIBS)
PKG_CHECK_MODULES(MONO_ADDINS_SETUP, mono-addins-setup >= $MONOADDINS_REQUIRED_VERSION)
AC_SUBST(MONO_ADDINS_SETUP_LIBS)
PKG_CHECK_MODULES(MONO_ADDINS_GUI, mono-addins-gui >= $MONOADDINS_REQUIRED_VERSION)
AC_SUBST(MONO_ADDINS_GUI_LIBS)
PKG_CHECK_MODULES(GLIB_SHARP, glib-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION)
AC_SUBST(GLIB_SHARP_LIBS)
PKG_CHECK_MODULES(GTK_SHARP, gtk-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION)
AC_SUBST(GTK_SHARP_LIBS)
PKG_CHECK_MODULES(GLADE_SHARP, glade-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION)
AC_SUBST(GLADE_SHARP_LIBS)

PKG_CHECK_MODULES(MONODOC, monodoc >= $MONODOC_REQUIRED_VERSION)
AC_SUBST(MONODOC_LIBS)
PKG_CHECK_MODULES(MONO_CAIRO, mono-cairo >= 1.2)
AC_SUBST(MONO_CAIRO_LIBS)

dnl soft dependencies
PKG_CHECK_MODULES(GNOME_SHARP, gnome-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION, [gnome_sharp=yes], [gnome_sharp=no])
AC_SUBST(GNOME_SHARP_LIBS)
PKG_CHECK_MODULES(GNOME_VFS_SHARP, gnome-vfs-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION, [gnome_vfs_sharp=yes], [gnome_vfs_sharp=no])
AC_SUBST(GNOME_VFS_SHARP_LIBS)
PKG_CHECK_MODULES(GCONF_SHARP, gconf-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION, [gconf_sharp=yes], [gconf_sharp=no])
AC_SUBST(GCONF_SHARP_LIBS)

gtksharp_prefix="`$PKG_CONFIG --variable=prefix gtk-sharp-2.0`"
AC_SUBST(gtksharp_prefix)

## nunit tests

AC_ARG_ENABLE(tests,
	AC_HELP_STRING([--enable-tests],
		[build unit tests [default=no]]),
		enable_tests=${enableval}, enable_tests=no)

AM_CONDITIONAL(ENABLE_TESTS, test x$enable_tests = xyes)

if test "x$enable_tests" = "xyes"; then
	PKG_CHECK_MODULES([MONO_NUNIT], [mono-nunit])
fi

AC_SUBST(MONO_NUNIT_LIBS)
AM_CONDITIONAL(ENABLE_TESTS, test x$enable_tests = xyes)

AC_ARG_ENABLE(monoextensions,
	AC_HELP_STRING([--enable-monoextensions],
		[enable extensions for Mono development [default=yes]]),
	enable_monoextensions=${enableval}, enable_monoextensions=yes)

AM_CONDITIONAL(ENABLE_MONOEXTENSIONS, test x$enable_monoextensions = xyes)

AC_ARG_ENABLE(versioncontrol,
	AC_HELP_STRING([--enable-versioncontrol],
		[enable version control support [default=yes]]),
		enable_versioncontrol=${enableval}, enable_versioncontrol=yes)

AM_CONDITIONAL(ENABLE_VERSIONCONTROL, test x$enable_versioncontrol = xyes)

AC_ARG_ENABLE(subversion,
	AC_HELP_STRING([--enable-subversion],
		[enable Subversion support [default=yes]]),
		enable_subversion=${enableval}, enable_subversion=${enable_versioncontrol})

if test x$enable_versioncontrol = xno && test x$enable_subversion = xyes; then
	AC_MSG_ERROR([Subversion addin cannot be built without Version Control addin])
fi

AM_CONDITIONAL(ENABLE_SUBVERSION, test x$enable_subversion = xyes)

platform_bindings=""

# Gnome platform addin
AC_ARG_ENABLE(gnomeplatform,
	AC_HELP_STRING([--enable-gnomeplatform],
		[enable Gnome platform support [default=yes]]),
		enable_gnomeplatform=${enableval}, enable_gnomeplatform=yes)

if test x$enable_gnomeplatform = xyes; then
	if test x$gnome_sharp = xno; then
		AC_MSG_ERROR([Cannot enable GNOME platform without gnome-sharp-2.0])
	fi
	if test x$gnome_vfs_sharp = xno; then
		AC_MSG_ERROR([Cannot enable GNOME platform without gnome-vfs-sharp-2.0])
	fi
	if test x$gconf_sharp = xno; then
		AC_MSG_ERROR([Cannot enable GNOME platform without gconf-sharp-2.0])
	fi
	platform_bindings="${platform_bindings}GNOME "
fi

AM_CONDITIONAL(ENABLE_GNOMEPLATFORM, [test x$enable_gnomeplatform = xyes])

# Mac platform addin
AC_ARG_ENABLE(macplatform,
	AC_HELP_STRING([--enable-macplatform],
		[enable Mac platform support [default=no]]),
		enable_macplatform=${enableval}, enable_macplatform=no)

if test x$enable_macplatform = xyes; then
	platform_bindings="${platform_bindings}Mac "
fi

AM_CONDITIONAL(ENABLE_MACPLATFORM, [test x$enable_macplatform = xyes])

# Windows platform addin
AC_ARG_ENABLE(windowsplatform,
	AC_HELP_STRING([--enable-windowsplatform],
		[enable Windows platform support [default=no]]),
		enable_windowsplatform=${enableval}, enable_windowsplatform=no)

if test x$enable_windowsplatform = xyes; then
	platform_bindings="${platform_bindings}Windows "
fi

AM_CONDITIONAL(ENABLE_WINDOWSPLATFORM, [test x$enable_windowsplatform = xyes])

# C/C++ project addin
AC_ARG_ENABLE(c, AC_HELP_STRING([--enable-c], [enable C/C++ project support [default=yes]]), enable_c=${enableval}, enable_c=yes)
AM_CONDITIONAL(ENABLE_C, [test x$enable_c = xyes])

############BEGIN UGLINESS#####################################################
# MonoDevelop doesn't depend on Mozilla, but some addins do. However, 
# due to Mozilla never being in the linker path, we have to put Mozilla 
# detection code in the MonoDevelop core so that MD can set LD_LIBRARY_PATH 
# MOZILLA_FIVE_HOME before starting.
###############################################################################
MOZFILE="libgtkembedmoz.so"

#Check pkg-config first
MOZILLA_HOME="`$PKG_CONFIG --variable=libdir mozilla-gtkmozembed`"
if test ! -z "$MOZILLA_HOME" && test ! -e "$MOZILLA_HOME/$MOZFILE";
		then AC_MSG_WARN(The mozilla-gtkmozembed.pc file is broken); unset MOZILLA_HOME;
	fi
if test -z "$MOZILLA_HOME"; then
	MOZILLA_HOME="`$PKG_CONFIG --variable=libdir firefox-gtkmozembed`"
	if test ! -z "$MOZILLA_HOME" && test ! -e "$MOZILLA_HOME/$MOZFILE";
		then AC_MSG_WARN(The firefox-gtkmozembed.pc file is broken); unset MOZILLA_HOME;
	fi
fi
if test -z "$MOZILLA_HOME"; then
	MOZILLA_HOME="`$PKG_CONFIG --variable=libdir xulrunner-gtkmozembed`"
	if test ! -z "$MOZILLA_HOME" && test ! -e "$MOZILLA_HOME/$MOZFILE";
		then AC_MSG_WARN(The xulrunner-gtkmozembed.pc file is broken); unset MOZILLA_HOME;
	fi
fi

#if MOZILLA_HOME is still empty, try to detect without pkg_config
#(NOTE: any changes to this block should be kept in sync with the one in monodevelop.in)
MOZFILE="libgtkembedmoz.so"
find_libgtkembedmoz ()
{
	MOZDIR=$MOZILLA_FIVE_HOME
	if test -e "$MOZDIR/$MOZFILE"; then echo $MOZDIR; return; fi
	
	MOZDIR=$(grep -h GRE_PATH= /etc/gre.d/*.conf 2>/dev/null | cut -d '"' -f 2 -d = | head -n 1) #"
	if test -e "$MOZDIR/$MOZFILE"; then echo $MOZDIR; return; fi
	
	mozilla_script=$(which mozilla 2> /dev/null)
	firefox_script=$(which firefox 2> /dev/null)
	
	for runtime_script in "$firefox_script $mozilla_script"; do
		MOZDIR=$(grep "MOZ_DIST_LIB=" $runtime_script 2> /dev/null | cut -d '"' -f 2 | cut -d '=' -f 2)
		if test -e "$MOZDIR/$MOZFILE"; then echo $MOZDIR; return; fi
		MOZDIR=$(grep "MOZILLA_FIVE_HOME=" $runtime_script 2> /dev/null | cut -d '"' -f 2 | cut -d '=' -f 2)
		if test -e "$MOZDIR/$MOZFILE"; then echo $MOZDIR; return; fi
		MOZDIR=$(grep "MOZILLA_LIBDIR=" $runtime_script 2> /dev/null | cut -d '"' -f 2 | cut -d '=' -f 2)
		if test -e "$MOZDIR/$MOZFILE"; then echo $MOZDIR; return; fi
		MOZDIR=$(grep "moz_libdir=" $runtime_script 2> /dev/null | cut -d '"' -f 2 | cut -d '=' -f 2)
		if test -e "$MOZDIR/$MOZFILE"; then echo $MOZDIR; return; fi
	done
}

if test -z "$MOZILLA_HOME"; then
	MOZILLA_HOME=`find_libgtkembedmoz`
fi

AC_SUBST(MOZILLA_HOME)

dnl Intl
ALL_LINGUAS="cs da de es fr ja pt_BR tr pl it zh_TW zh_CN ru sl ca gl pt sv nl id hu"
AC_SUBST(ALL_LINGUAS)
GETTEXT_PACKAGE=monodevelop
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package])

CSC_FLAGS="-debug -codepage:utf8"

AC_SUBST(LIB_SUFFIX)
AC_SUBST(LIB_PREFIX)
AC_SUBST(ASSEMBLY_VERSION)
AC_SUBST(PACKAGE_VERSION_LABEL)
AC_SUBST(SQLITE_XML)
AC_SUBST(CSC_FLAGS)


MD_DIR='$(prefix)/lib/monodevelop'
MD_ASSEMBLY_DIR="$MD_DIR/bin"
MD_ADDIN_DIR="$MD_DIR/AddIns"

AC_SUBST(MD_ASSEMBLY_DIR)
AC_SUBST(MD_ADDIN_DIR)
AC_SUBST(MD_DIR)

if test -z "$platform_bindings"; then
	AC_MSG_ERROR([You must enable one of the platform bindings])
fi

AC_OUTPUT([
build/data/Makefile
build/data/resources/css/Makefile
build/data/resources/Makefile
build/Makefile
contrib/Makefile
contrib/Mono.Cecil/Makefile
src/Makefile
src/core/Makefile
src/core/NRefactory/Makefile
src/core/Mono.Debugging/Makefile
src/core/MonoDevelop.Core/AssemblyInfo.cs
src/core/MonoDevelop.Core/Makefile
src/core/MonoDevelop.Core.Gui/AssemblyInfo.cs
src/core/MonoDevelop.Core.Gui/Makefile
src/core/MonoDevelop.Projects/AssemblyInfo.cs
src/core/MonoDevelop.Projects/MonoDevelop.Projects.dll.config
src/core/MonoDevelop.Projects/Makefile
src/core/MonoDevelop.Projects.Gui/AssemblyInfo.cs
src/core/MonoDevelop.Projects.Gui/Makefile
src/core/MonoDevelop.Ide/AssemblyInfo.cs
src/core/MonoDevelop.Ide/Makefile
src/core/MonoDevelop.Dock/Makefile
src/core/MonoDevelop.Components/AssemblyInfo.cs
src/core/MonoDevelop.Components/Makefile
src/core/MonoDevelop.Startup/AssemblyInfo.cs
src/core/MonoDevelop.Startup/Makefile
src/tools/Makefile
src/tools/mdhost/Makefile
src/tools/mdrun/Makefile
src/tools/SharpCoco/Makefile
src/addins/Makefile
src/addins/MonoDeveloperExtensions/Makefile
src/addins/MonoDeveloperExtensions/NUnit/Makefile
src/addins/NUnit/Makefile
src/addins/VersionControl/Diff/Makefile
src/addins/VersionControl/DiffWidget/Makefile
src/addins/VersionControl/MonoDevelop.VersionControl/Makefile
src/addins/VersionControl/MonoDevelop.VersionControl.Subversion/Makefile
src/addins/VersionControl/Makefile
src/addins/CSharpBinding/Makefile
src/addins/CSharpBinding/Autotools/Makefile
src/addins/ILAsmBinding/Makefile
src/addins/VBNetBinding/Makefile
src/addins/prj2make-sharp-lib/AssemblyInfo.cs
src/addins/prj2make-sharp-lib/Makefile
src/addins/WelcomePage/Makefile
src/addins/ChangeLogAddIn/Makefile
src/addins/MonoDevelop.GtkCore/Makefile
src/addins/MonoDevelop.GtkCore/libstetic/Makefile
src/addins/MonoDevelop.GtkCore/libsteticui/Makefile
src/addins/AspNetAddIn/Makefile
src/addins/MonoDevelop.Autotools/Makefile
src/addins/MonoDevelop.DesignerSupport/Makefile
src/addins/MonoDevelop.WebReferences/Makefile
src/addins/Deployment/Makefile
src/addins/Deployment/MonoDevelop.Deployment/Makefile
src/addins/Deployment/MonoDevelop.Deployment.Linux/Makefile
src/addins/MonoDevelop.Gettext/Makefile
src/addins/MonoDevelop.RegexToolkit/Makefile
src/addins/CBinding/Makefile
src/addins/GnomePlatform/Makefile
src/addins/MacPlatform/Makefile
src/addins/WindowsPlatform/Makefile
src/addins/MonoDevelop.AssemblyBrowser/Makefile
src/addins/Mono.Texteditor/Makefile
src/addins/MonoDevelop.SourceEditor2/Makefile
src/addins/MonoDevelop.Moonlight/Makefile
src/addins/MonoDevelop.XmlEditor/Makefile
src/addins/MonoDevelop.CodeMetrics/Makefile
src/addins/MonoDevelop.Debugger/Makefile
tests/Makefile
tests/UnitTests/Makefile
Makefile
monodevelop
mdtool
monodevelop.pc
monodevelop-core-addins.pc
po/Makefile
man/Makefile
theme-icons/Makefile
])

echo ""
echo "Configuration summary"
echo ""
echo "   * Installation prefix = $prefix"
echo "   * C# compiler = $CSC"
echo "   * Mono class library development extensions: $enable_monoextensions"
echo "   * Version control support: $enable_versioncontrol"
echo "   *   Providers:"
echo "   *     Subversion: $enable_subversion"
echo "   * C/C++ project support: $enable_c"
echo "   * Platform bindings: $platform_bindings"
echo "   * Unit tests: $enable_tests"
echo "   * Mozilla location: $MOZILLA_HOME"
echo ""
